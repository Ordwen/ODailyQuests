plugins {
    id 'java'
}

group = 'com.ordwen'
version = '2.2.6-SNAPSHOT-1'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://repo.rosewooddev.io/repository/public/'
    }
    maven {
        name = 'jitpack-repo'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'everything'
        url = 'https://repo.citizensnpcs.co/'
    }
    maven {
        name = ' Lumine Releases'
        url = 'https://mvn.lumine.io/repository/maven-public/'
    }
    maven {
        name = 'minecraft-repo'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = 'bg-repo'
        url = 'https://repo.bg-software.com/repository/api/'
    }
    maven {
        url = 'https://repo.rosewooddev.io/repository/public/'
    }
    maven {
        name = "phoenix"
        url = "https://nexus.phoenixdevt.fr/repository/maven-public/"
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {

    //implementation "net.kyori:adventure-text-minimessage:4.14.0"
    //implementation "net.kyori:adventure-platform-bukkit:4.3.0"

    /* database management */
    compileOnly 'com.zaxxer:HikariCP:5.0.1'
    compileOnly 'com.h2database:h2:2.1.214'

    /* spigot */
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    /* economy & points */
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'org.black_ixx:playerpoints:3.2.2'
    compileOnly name: 'CoinsEngine-2.0.5'
    compileOnly ('com.github.Realizedd:TokenManager:3.2.4') { transitive = false }

    /* npcs */
    compileOnly 'net.citizensnpcs:citizens-main:2.0.29-SNAPSHOT'

    /* mobs */
    compileOnly 'io.lumine:Mythic-Dist:5.0.3-SNAPSHOT'
    compileOnly name: 'EliteMobs'

    /* stackers */
    compileOnly 'dev.rosewood:rosestacker:1.4.13'
    compileOnly 'com.bgsoftware:WildStackerAPI:3.8.0'

    /* guis */
    compileOnly 'me.clip:placeholderapi:2.10.0'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.9'

    /* custom items */
    compileOnly 'com.github.oraxen:oraxen:1.157.0'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.5.0b'
    compileOnly 'net.Indyuce:MMOItems-API:6.9.2-SNAPSHOT'
    compileOnly 'io.lumine:MythicLib-dist:1.5.2-SNAPSHOT'

    /* auto restart */
    implementation 'com.github.N0RSKA:UltimateAutoRestartAPI:2023.12'

    /* Mojang auth API for heads */
    compileOnly 'com.mojang:authlib:1.5.21'
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
